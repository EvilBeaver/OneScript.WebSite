

&Пластилин
Перем РегистраторКаталогаФайлов;

&Пластилин
Перем МенеджерОтображений;

&Рогатка
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

Процедура ПриЗапускеПриложения() Экспорт

	Каталог = ОбъединитьПути(ТекущийКаталог(), "..");

	Каталог = Новый Файл(Каталог).ПолноеИмя;

	КаталогФронта = ОбъединитьПути(Каталог, "files", "wwwroot");

	РегистраторКаталогаФайлов.ДобавитьКаталогСтатичныхФайлов(КаталогФронта, "/");
	РегистраторКаталогаФайлов.ДобавитьКаталогСтатичныхФайлов(ОбъединитьПути(Каталог, "files", "download"), "/downloads");

	ФайлыИндексов = ФайлыИндексов(КаталогФронта);

	Для Каждого ФайлИндекса Из ФайлыИндексов Цикл
		РегистраторКаталогаФайлов.РегистрацияФайла(ФайлИндекса.Путь, 
					ФайлИндекса.Расположение, 
					"text/html; charset=utf-8");

	КонецЦикла;

	// РегистраторКаталогаФайлов.РегистрацияФайла("/", 
	// 	ОбъединитьПути(КаталогФронта, "index.html"), 
	// 	"text/html; charset=utf-8");

	ЧтениеТекста = Новый ЧтениеТекста(ОбъединитьПути(КаталогФронта, "404.html"));
	Текст404 = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	МенеджерОтображений.УстановитьШаблон404(Текст404);

КонецПроцедуры

Функция ФайлыИндексов(Каталог)
	ФайлыИндексы = НайтиФайлы(Каталог, "index.html", Истина);

	Результат = Новый Массив();
	Для каждого Файл Из ФайлыИндексы Цикл
		Путь = СтрЗаменить(Файл.Путь, Каталог, "");
		Путь = СтрЗаменить(Путь, "\", "/");
		Путь = "/" + СтрСоединить(СтрРазделить(Путь, "/", Ложь), "/");
		Результат.Добавить(ОписаниеФайла(Путь, Файл.ПолноеИмя));
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ОписаниеФайла(Путь, Расположение)
	Возврат Новый Структура("Путь, Расположение", Путь, Расположение);
КонецФункции